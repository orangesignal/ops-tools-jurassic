#!/usr/bin/expect -f
# deprecated
log_user 0

if { [llength $argv] == 0 } {
  puts "Usage: expect -f $argv0 -- \'ssh_args\' hostname username \'password\' root_cmd \'root_password\' service_name command"
  exit 0
}

set self_dir [file dirname $argv0]
source "$self_dir/proc.tcl"

###############################################################################
# procedure

proc createCommand { argv start } {
  set _command  [lindex $argv $start]
  for { set _index [expr $start + 1] } { $_index < [llength $argv] } { incr _index } {
    append _command " [defaultString [lindex $argv $_index] '']"
  }
  return "$_command"
}

###############################################################################
# variables

set env(TERM) vt100
set env(LANG) C
set env(SHELL) /bin/sh
set env(HOME) /usr/local/bin

set multiPrompt {[#>$] }
set pw_prompt {[:]}

# parse args
set ssh_args      [lindex $argv 0]
set hostname      [lindex $argv 1]
set username      [lindex $argv 2]
set password      [lindex $argv 3]
set root_cmd      [lindex $argv 4]
set root_password [lindex $argv 5]
set service_name  [lindex $argv 6]
set cmd           [createCommand $argv 7]

# validation
assertNotEmpty $hostname     "hostname must not be null\n"
assertNotEmpty $username     "username must not be null\n"
assertNotEmpty $root_cmd     "root_cmd must not be null\n"
assertNotEmpty $service_name "service_name must not be null\n"
assertNotEmpty $cmd          "cmd must not be null\n"

###############################################################################
# spawn - ssh login + switch to root

set timeout 10
eval spawn -noecho ssh $ssh_args -t $username@$hostname $root_cmd

expect {
  eof     { abort "SSH failure for remote host $hostname\n" 255 }
  timeout { abort "Failed to get password prompt\n" 255 }
  *assword: {
    send -- "$password\r"
  }
}
expect {
  timeout { abort "Login failed. Password incorrect.\n" 5 }
  -re $multiPrompt {
    # sudo NOPASSWD
  }
  : {
    send -- "$root_password\r"
    expect {
      timeout { abort "Failed to Switch Root.\n" 5 }
      -re $multiPrompt
    }
  }
}

###############################################################################
# operation (root)

set prompt [getPrompt $expect_out(buffer) $multiPrompt]
set timeout -1

# execute service command
send -- "service $service_name $cmd\r"
expect {
  -re $prompt
}

# command status
send -- "echo \$?\r"
expect {
  -re "\n(\[^\r]*)\r"
}
set cmd_status "$expect_out(1,string)"

expect {
  -re $prompt
}

# get process count
sleep 1
send -- "ps ax | grep $service_name | grep -v grep | grep -c ''\r"
expect {
  -re "\n(\[^\r]*)\r"
}
send_user "$expect_out(1,string)"

expect {
  -re $prompt
}

# root exit
send -- "exit\r"

###############################################################################
# terminate

expect eof
catch wait result
set os_error [lindex $result 2]
if { $os_error == -1 } {
  send_error "Fail to exec\n"
  exit 127
}
set status [lindex $result 3]
if { $status == 0 } {
  exit $cmd_status
}
exit $status
