#!/usr/bin/expect -f

set env(TERM) vt100
set env(LANG) C

set prompt {[#>$]}
set pw_prompt {[:]}

set ssh_config    [lindex $argv 0]
set hostname      [lindex $argv 1]
set username      [lindex $argv 2]
set password      [lindex $argv 3]
set root_cmd      [lindex $argv 4]
set root_password [lindex $argv 5]
set service_name  [lindex $argv 6]
set cmd           [lindex $argv 7]

# ssh login + switch to root
log_user 0
set timeout 10
spawn ssh -F ${ssh_config} -q -t -t $username@$hostname $root_cmd
expect {
  eof     { send_error "SSH failure for host"; exit 1 }
  timeout { send_error "Failed to get password prompt"; exit 1 }
  password: 
}
send -- "$password\r"
expect {
  timeout { send_error "Unable to access the host"; exit 1 }
  : 
}
send -- "$root_password\r"
expect {
  timeout { send_error "Login failed. Password incorrect."; exit 1 }
  -re $prompt
}

set timeout -1

# execute service command
send -- "service $service_name $cmd\r"
expect {
  -re $prompt
}

# command status
send -- "echo \$?\r"
expect {
  -re "\n(\[^\r]*)\r"
}
set cmd_status "$expect_out(1,string)"

expect {
  -re $prompt
}

# get process count
sleep 1
send -- "ps ax | grep $service_name | grep -v grep | grep -c ''\r"
expect {
  -re "\n(\[^\r]*)\r"
}
send_user "$expect_out(1,string)"

expect {
  -re $prompt
}

# root exit
send -- "exit\r"

# terminate
expect eof
catch wait result
set os_error [lindex $result 2]
if { $os_error == -1 } {
  puts "Fail to exec"
  exit 127
}
set status [lindex $result 3]
if { $status == 0 } {
  exit $cmd_status
}
exit $status
