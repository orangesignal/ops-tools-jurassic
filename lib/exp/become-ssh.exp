#!/usr/bin/expect -f

if { [llength $argv] == 0 } {
  puts "Usage: expect -f $argv0 -- \'password\' \'become_password\' \'become_cmd\' \'ssh_cmd\' \'ssh_args\' \'remote_file\' \[command...\]"
  exit 0
}

set self_dir [file dirname $argv0]
source "$self_dir/procedures"

###############################################################################
# procedure

proc createCommand { argv start } {
  set _remote_file  [lindex $argv $start]
  set _command      [lindex $argv [expr $start + 1]]
  for { set _index [expr $start + 2] } { $_index < [llength $argv] } { incr _index } {
    append _command " [defaultString [lindex $argv $_index] '']"
  }
  if { $_remote_file ne ""  } {
    return "cat $_remote_file | $_command"
  }
  return "$_command"
}

###############################################################################
# variables

# parse args
set password        [lindex $argv 0]
set become_password [defaultString [lindex $argv 1] $password]
set become_cmd      [defaultString [lindex $argv 2] {sudo su -}]
set ssh_cmd         [defaultString [lindex $argv 3] [exec which ssh]]
set ssh_args        [lindex $argv 4]
set cmd             [createCommand $argv 5]

###############################################################################
# spawn - ssh login + switch to root

# BUGS - http://www.tcl.tk/man/expect5.31/expect.1.html
if { ![info exists env(TERM)] } { set env(TERM) vt100 }
if { ![info exists env(SHELL)] } { set env(SHELL) /bin/sh }
if { ![info exists env(HOME)] } { set env(HOME) /usr/local/bin }
if { ![info exists env(LANG)] } { set env(LANG) C }

set timeout 10
eval spawn -noecho $ssh_cmd -t $ssh_args $become_cmd

set multiPrompt {[^\n]*[#>$] $}
set password_prompt {[^\n]*[pP]assword: $}
set become_password_prompt {[^\n]*:[ ]*$}

if { $cmd ne "" } {
  log_user 0
}

expect {
  eof     { abort "${argv0}: SSH failure for remote host\n" 255 }
  timeout { abort "${argv0}: Failed to get password prompt\n" 255 }
  -glob "(yes/no)?" {
    send -- "yes\r"
    exp_continue
  }
  -re $password_prompt {
    send -- "$password\r"
  }
}
expect {
  timeout { abort "${argv0}: Login failed. Password incorrect.\n" 5 }
  -re $multiPrompt {
    # sudo NOPASSWD
  }
  -re $become_password_prompt {
    send -- "$become_password\r"
    expect {
      timeout { abort "${$argv0}: Failed to Switch Root.\n" 5 }
      -re $multiPrompt
    }
  }
}

###############################################################################
# operation (root)

set timeout -1

if { $cmd eq "" } {
  interact
  exit 0
}

set prompt "[reEscape $expect_out(0,string)]$"

send -- " export HISTCONTROL=ignorespace\r"
expect {
  -re $prompt
}

# execute command
send -- " ${cmd}\r"
expect {
  -re "\n(.*\r).*$prompt" {
    set msg [string trimright "$expect_out(1,string)" "\r"]
    send_user -- "$msg\n"
  }
  -re $prompt
}

# command status
send -- " echo \$?\r"
expect {
  -re "\n(\[^\r]*)\r"
}
set cmd_status "$expect_out(1,string)"

expect {
  -re $prompt
}

# root exit
send -- " exit\r"

###############################################################################
# terminate

expect eof
catch wait result
set os_error [lindex $result 2]
if { $os_error == -1 } {
  send_error -- "${argv0}: Fail to exec\n"
  exit 127
}
set status [lindex $result 3]
if { $status == 0 } {
  exit $cmd_status
}
exit $status